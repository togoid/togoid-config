PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gene: <http://purl.uniprot.org/geneid/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX edam: <http://edamontology.org/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?gene_id ?uniprot_id
WHERE {
  GRAPH <http://rdf.glycoinfo.org/ggdb> {
    ?s a <http://purl.uniprot.org/core/Gene> .
    ?s sio:SIO_000008 ?gene .
    ?gene a edam:data_2295 ;  # Gene ID
          dcterms:identifier ?gene_id .
    BIND (URI(CONCAT("http://purl.uniprot.org/geneid/", STR(?gene_id))) AS ?up_gene)
  }

  GRAPH <http://rdf.glycosmos.org/uniprot_geneid> {
    ?protein rdfs:seeAlso ?up_gene .
  }
  BIND(STRAFTER(STR(?protein), "http://purl.uniprot.org/uniprot/") AS ?uniprot_id)
}
