PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oboinowl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT ?child_id ?uberon_id
FROM <http://purl.obolibrary.org/obo/merged/DOID>
WHERE {
  ?do owl:equivalentClass / owl:intersectionOf / (rdf:rest)* / (rdf:first)? ?o .
  ?o a owl:Restriction ;
     owl:onProperty obo:RO_0004026 ; # disease has location
     owl:someValuesFrom ?uberon .
  ?child rdfs:subClassOf* ?do .
  BIND(STRAFTER(STR(?child), "http://purl.obolibrary.org/obo/DOID_") AS ?child_id)
  BIND(STRAFTER(STR(?uberon), "http://purl.obolibrary.org/obo/UBERON_") AS ?uberon_id)
}
