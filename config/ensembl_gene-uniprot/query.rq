# Endpoint: https://rdfportal.org/ebi/sparql
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX taxon: <http://identifiers.org/taxonomy/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ensembl: <http://rdf.ebi.ac.uk/terms/ensembl/>

# GRAPH varies e.g., http://rdf.ebi.ac.uk/dataset/ensembl/102/homo_sapiens for tax_id 9606
SELECT DISTINCT ?ensg_id ?uniprot_id
WHERE {
  {
    SELECT DISTINCT ?ensg_id ?uniprot
    WHERE {
      VALUES ?taxon { taxon:{{taxon}} }
      ?ensg obo:RO_0002162 ?taxon ;
            dc:identifier ?ensg_id .
      ?enst obo:SO_transcribed_from ?ensg ;
            obo:SO_translates_to / rdfs:seeAlso ?uniprot .
      ?uniprot a ido:uniprot .
    }
  }
  BIND(STRAFTER(STR(?uniprot), "uniprot/") AS ?uniprot_id)
}
