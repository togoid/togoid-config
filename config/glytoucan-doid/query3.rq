PREFIX ggdont: <http://jcggdb.jp/rdf/diseases/ggdonto-schema#>
PREFIX gmncbi: <http://jcggdb.jp/rdf/diseases/gmncbi-schema#>
PREFIX glycan: <http://purl.jp/bio/12/glyco/glycan#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ggdb: <http://purl.jp/bio/12/ggdb/2015/6/owl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT ?do ?ag ?pg
WHERE{
  GRAPH <http://rdf.glycoinfo.org/ggdonto> {
    [ a ggdont:DiseaseDescriptionGGDonto ;
      ggdont:conceptUI ?concept_ui ;
      gmncbi:causedBy ?gene ;
      rdfs:seeAlso ?omim ]
    FILTER CONTAINS(str(?omim), "omim")
    FILTER CONTAINS(str(?gene), "ncbigene")
    BIND(concat("OMIM:",substr(str(?omim),29)) as ?omim_str)
    BIND (xsd:int(substr(str(?gene), 33)) as ?gene_id)
  }
  GRAPH <http://purl.obolibrary.org/obo/doid> { ?do oboInOwl:hasDbXref ?omim_str1 . BIND(STR(?omim_str1) AS ?omim_str)}

  GRAPH <http://rdf.glycoinfo.org/ggdb> {
    [] a ggdb:Glycogene ;
       ggdb:has_acceptor_substrates / ggdb:has_reaction [
         a <http://purl.jp/bio/12/ggdb/2015/6/owl#General_reaction> ;
#         ggdb:has_donor / rdfs:label ?doner ;
         ggdb:has_acceptor / dcterms:identifier ?aid ;
         ggdb:has_product / dcterms:identifier ?pid ] ;
       ggdb:has_gene / sio:SIO_000008 [
         a <http://edamontology.org/data_2295> ;
         dcterms:identifier ?gene_id ] .
  }
  GRAPH <http://rdf.glytoucan.org/partner/jcggdb_aist> {
    ?ag glycan:has_resource_entry / dcterms:identifier ?aid .
    OPTIONAL {?pg glycan:has_resource_entry / dcterms:identifier ?pid }
  }
}
