# Endpoint: https://integbio.jp/rdf/ebi/sparql
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

SELECT DISTINCT ?source ?ensg_id
FROM <http://rdf.ebi.ac.uk/dataset/chembl>
WHERE {
  ?mechanism a cco:Mechanism ;
    cco:hasTarget ?target ;
    cco:hasMolecule ?molecule .
  ?target cco:hasTargetComponent ?component .
  ?component cco:targetCmptXref ?ensg_uri .
  ?ensg_uri a cco:EnsemblGeneRef .
  ?molecule cco:chemblId ?chembl_id .
  BIND (strafter(str(?ensg_uri), 'http://identifiers.org/ensembl/') AS ?ensg_id)
  BIND (strafter(str(?target), 'http://rdf.ebi.ac.uk/resource/chembl/target/') AS ?source)
}
