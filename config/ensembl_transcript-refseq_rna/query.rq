# Endpoint: https://rdfportal.org/ebi/sparql
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX taxon: <http://identifiers.org/taxonomy/>
PREFIX so: <http://purl.obolibrary.org/obo/so#>

SELECT DISTINCT ?enst_id ?refseq_id
FROM <http://rdfportal.org/dataset/ensembl>
WHERE {
  VALUES ?taxon { taxon:{{taxon}} }
  ?ensg obo:RO_0002162 ?taxon .
  ?enst so:transcribed_from ?ensg ;
        dcterms:identifier ?enst_id .
  ?enst rdfs:seeAlso ?refseq .
  FILTER (STRSTARTS (STR(?refseq), "http://identifiers.org/refseq/"))
  BIND(REPLACE(STRAFTER(STR(?refseq), "refseq/"), "\\.[0-9]+", "") AS ?refseq_id)
}
