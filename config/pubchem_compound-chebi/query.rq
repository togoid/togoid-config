PREFIX sio: <http://semanticscience.org/resource/>
PREFIX cheminf: <http://semanticscience.org/resource/>
PREFIX source: <http://rdf.ncbi.nlm.nih.gov/pubchem/source/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX pubchemv: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>

SELECT DISTINCT ?pubchem_id ?chebi_id
FROM <http://rdf.ncbi.nlm.nih.gov/pubchem/substance>
WHERE {
  ?substance a pubchemv:Substance ;
             a ?chebi_class ;
             cheminf:CHEMINF_000477 ?cid ; # has PubChem normalized counterpart
             dcterms:source source:ChEBI .
  FILTER(?chebi_class != pubchemv:Substance)
  BIND (STRAFTER(STR(?chebi_class), "http://purl.obolibrary.org/obo/CHEBI_") AS ?chebi_id)
  BIND (STRAFTER(STR(?cid), "http://rdf.ncbi.nlm.nih.gov/pubchem/compound/CID") AS ?pubchem_id)
}
