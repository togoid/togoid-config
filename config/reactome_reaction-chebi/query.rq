# Endpoint: https://integbio.jp/rdf/ebi/sparql
PREFIX biopax: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?reaction ?chebi_id
FROM <http://rdf.ebi.ac.uk/dataset/reactome>
WHERE {
  ?react
    biopax:xref [
      biopax:db 'Reactome'^^xsd:string ;
      biopax:id ?reaction
    ] ;
    biopax:left|biopax:right|biopax:product|^biopax:controlled/biopax:controller ?component .
  ?component
    (biopax:memberPhysicalEntity|biopax:component)*/biopax:entityReference/biopax:xref [
      biopax:db 'ChEBI'^^xsd:string ;
      biopax:id ?chebi
    ] .
  BIND (strafter(?chebi, "CHEBI:") AS ?chebi_id)
}


