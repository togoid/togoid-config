# Endpoint: https://rdfportal.org/ebi/sparql
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

SELECT DISTINCT ?source ?xref_id
FROM <http://rdf.ebi.ac.uk/dataset/chembl>
WHERE {
  ?mechanism a cco:Mechanism ;
    cco:hasTarget ?target ;
    cco:hasMolecule ?molecule .
  ?molecule cco:chemblId ?chembl_id .
  ?target cco:hasTargetComponent ?component .
  ?component cco:targetCmptXref ?xref_uri .
  ?xref_uri a cco:ProteinDataBankRef .
  BIND (ucase(strafter(str(?xref_uri), 'http://identifiers.org/pdb/')) AS ?xref_id)
  BIND (ucase(strafter(str(?target), 'http://rdf.ebi.ac.uk/resource/chembl/target/')) AS ?source)
}