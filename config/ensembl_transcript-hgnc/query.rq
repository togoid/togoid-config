# Endpoint: https://rdfportal.org/ebi/sparql
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX taxon: <http://identifiers.org/taxonomy/>
PREFIX so: <http://purl.obolibrary.org/obo/so#>

SELECT DISTINCT ?enst_id ?hgnc_id
FROM <http://rdfportal.org/dataset/ensembl>
WHERE {
  ?ensg obo:RO_0002162 taxon:9606 ;
        rdfs:seeAlso ?hgnc .
  ?enst so:transcribed_from ?ensg ;
        dcterms:identifier ?enst_id .
  FILTER (STRSTARTS (STR(?hgnc), "http://identifiers.org/hgnc/"))
  BIND (STRAFTER(STR(?hgnc), 'HGNC:') AS ?hgnc_id)
}
